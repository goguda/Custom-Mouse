; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#include <idp.iss>

#define MyAppName "Custom Mouse Controller"
#define MyAppVersion "1.1"
#define MyAppPublisher "David Goguen"
#define MyAppURL "https://github.com/goguda/Custom-Mouse/releases"
#define MyAppExeName "Custom Mouse Controller.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{1743E12B-29AE-47CD-987D-BEDCD0F33B24}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=Custom_Mouse_Controller_1.1_Setup
ArchitecturesInstallIn64BitMode=x64
Compression=lzma
SolidCompression=yes
AppMutex="Custom Mouse Controller"

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Code]
function Framework471IsNotInstalled(): Boolean;
var
  bSuccess: Boolean;
  regVersion: Cardinal;
begin
  Result := True;

  bSuccess := RegQueryDWordValue(HKLM, 'Software\Microsoft\NET Framework Setup\NDP\v4\Full', 'Release', regVersion);
  if (True = bSuccess) and (regVersion >= 461308) then begin
    Result := False;
  end;
end;

procedure InitializeWizard;
begin
  if Framework471IsNotInstalled() then
  begin
    idpAddFile('https://download.microsoft.com/download/8/E/2/8E2BDDE7-F06E-44CC-A145-56C6B9BBE5DD/NDP471-KB4033344-Web.exe', ExpandConstant('{tmp}\NetFrameworkInstaller.exe'));
    idpDownloadAfter(wpReady);
  end;
end;

procedure InstallFramework;
var
  StatusText: string;
  ResultCode: Integer;
begin
  StatusText := WizardForm.StatusLabel.Caption;
  WizardForm.StatusLabel.Caption := 'Downloading and installing .NET Framework 4.7.1. This might take a few minutes...';
  WizardForm.ProgressGauge.Style := npbstMarquee;
  try
    if not Exec(ExpandConstant('{tmp}\NetFrameworkInstaller.exe'), '/passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
    begin
      MsgBox('.NET installation failed with code: ' + IntToStr(ResultCode) + '.', mbError, MB_OK);
    end;
  finally
    WizardForm.StatusLabel.Caption := StatusText;
    WizardForm.ProgressGauge.Style := npbstNormal;

    DeleteFile(ExpandConstant('{tmp}\NetFrameworkInstaller.exe'));
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  case CurStep of
    ssPostInstall:
      begin
        if Framework471IsNotInstalled() then
        begin
          InstallFramework();
        end;
      end;
  end;
end;

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "C:\Users\david\Documents\Visual Studio 2017\Projects\Custom-Mouse\CustomMouseController\bin\x86\Release\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs; Check: not Is64BitInstallMode
Source: "C:\Users\david\Documents\Visual Studio 2017\Projects\Custom-Mouse\CustomMouseController\bin\x64\Release\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs; Check: Is64BitInstallMode
Source: "C:\Program Files (x86)\Arduino\drivers\*"; DestDir: "{app}\drivers"; Flags: ignoreversion recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\drivers\dpinst-x86.exe"; Parameters: "/A /LM"; Check: not Is64BitInstallMode
Filename: "{app}\drivers\dpinst-amd64.exe"; Parameters: "/A /LM"; Check: Is64BitInstallMode
Filename: "schtasks"; Parameters: "/Create /F /SC ONLOGON /RL HIGHEST /TN ""Custom Mouse Controller"" /TR ""'{app}\Custom Mouse Controller.exe'"""; Flags: runhidden
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent runascurrentuser

[UninstallRun]
Filename: "schtasks"; Parameters: "/Delete /F /TN ""Custom Mouse Controller"""; Flags: runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{localappdata}\Custom Mouse Controller"